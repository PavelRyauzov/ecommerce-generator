Функция ПолучитьДанныеНоменклатуры() Экспорт
	
	// Характеристики номенклатуры
	Запрос = Новый Запрос;
	Запрос.Текст =  
	"ВЫБРАТЬ 
	| ХарактеристикиНоменклатуры.Ссылка КАК Ссылка,
	| ХарактеристикиНоменклатуры.Владелец КАК Владелец,
	| ХарактеристикиНоменклатуры.Наименование КАК Наименование,
	| ХарактеристикиНоменклатуры.НаименованиеПолное КАК НаименованиеПолное
	|ИЗ 
	|   Справочник.ХарактеристикиНоменклатуры КАК ХарактеристикиНоменклатуры";
	
	Выборка = Запрос.Выполнить().Выбрать();  
	
	МассивХарактеристикиНоменклатуры = Новый Массив;
	Пока Выборка.Следующий()Цикл
		ХарактеристикаНоменклатуры = Новый Структура;
		ХарактеристикаНоменклатуры.Вставить("external_id", "" + Выборка.Ссылка.УникальныйИдентификатор());
		ХарактеристикаНоменклатуры.Вставить("product_id", "" + Выборка.Владелец. УникальныйИдентификатор());
		ХарактеристикаНоменклатуры.Вставить("title", Выборка.Наименование);
		ХарактеристикаНоменклатуры.Вставить("description", Выборка.НаименованиеПолное);
		МассивХарактеристикиНоменклатуры.Добавить(ХарактеристикаНоменклатуры);
	КонецЦикла;
	
	// Виды номенклатуры
	Запрос.Текст = 
	"ВЫБРАТЬ 
	| ВидыНоменклатуры.Ссылка КАК Ссылка,
	| ВидыНоменклатуры.Наименование КАК Наименование
	|ИЗ 
	|   Справочник.ВидыНоменклатуры КАК ВидыНоменклатуры";

	Выборка = Запрос.Выполнить().Выбрать();
	
	МассивВидыНоменклатуры = Новый Массив;
	Пока Выборка.Следующий()Цикл
		ВидНоменклатуры = Новый Структура;
		ВидНоменклатуры.Вставить("external_id", "" + Выборка.Ссылка.УникальныйИдентификатор());
		ВидНоменклатуры.Вставить("name", "" + Выборка.Наименование);
		МассивВидыНоменклатуры.Добавить(ВидНоменклатуры);
	КонецЦикла;

	КорневойОбъект = Новый Структура;
	КорневойОбъект.Вставить("product_characteristic", МассивХарактеристикиНоменклатуры);
	КорневойОбъект.Вставить("product_type", МассивВидыНоменклатуры);
		
	ЗаписьJSON = Новый ЗаписьJSON;
	ЗаписьJSON.УстановитьСтроку();
	ЗаписатьJSON(ЗаписьJSON, КорневойОбъект);
	СтрокаJSON = ЗаписьJSON.Закрыть();
	
	Возврат СтрокаJSON;
	
КонецФункции